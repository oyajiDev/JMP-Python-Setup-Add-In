NamesDefaultToHere(1);

gdd = GetDefaultDirectory();
If (Hostis("Windows"),
    gdd = ConvertFilePath(gdd, windows);
);

win = NewWindow("JMP-Python Setup",
	TabBox(
		TabPageBox(
			Title("Setup"),
			HListBox(
				SpacerBox(),
				VListBox(
					SpacerBox(),
					tbProcess = TextBox("Prepare",
						<< JustifyText("center");
					),
					SpacerBox(),
					btnStart = ButtonBox("Start",
						PythonSubmitFile(gdd || "scripts/setup.py");
					),
					btnClose = ButtonBox("Close",
						win << CloseWindow;
					),
					SpacerBox()
				),
				SpacerBox()
			)
		),
		TabPageBox(
			Title("Requirements"),
			HListBox(
				SpacerBox(),
				VListBox(
					cbRequire = CheckBox({ "use file" },
						If (cbRequire << Get(),
							bhList << Visibility("Collapse");
							hbFile << Visibility("Visible");
						,
							bhList << Visibility("Visible");
							hbFile << Visibility("Collapse");
						);
						
						win << SetWindowSize(400, 200);
					),
					SpacerBox(),
					hbFile = HListBox(
						ButtonBox("Select",
							tbFile << SetText(PickFile(
								"Select requirements file",
								"$DOCUMENTS",
								{ "Requirements File|txt" }
							));
						),
						tbFile = TextBox("",
							<< JustifyText("left");
						)
					),
					bhList = HListBox(
						sbList = ScriptBox(
							"# list of requirements\!N",
							"Text"
						)
					),
					SpacerBox(),
					HListBox(
						SpacerBox(),
						tbRequireProgress = TextBox("",
							<< JustifyText("left");
						),
						ButtonBox("Install",
							useFile = cbRequire << Get();
							reqList = sbList << GetText();
							reqFile = tbFile << GetText();

							PythonSend(gdd);
							PythonSend(useFile);
							PythonSend(reqList);
							PythonSend(reqFile);
							PythonSubmitFile(gdd || "scripts/install_requires.py");
						)
					),
					SpacerBox()
				)
			)
		)
	),
	btnClose << Visibility("Collapse");
	hbFile << Visibility("Collapse");
);
win << SetWindowSize(400, 200);
